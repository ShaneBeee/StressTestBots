import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.7"
    id 'maven-publish'
}

group 'com.shanebeestudios'
version '1.0.0'
def mcProtocolLibVer = '1.20.6-2-SNAPSHOT'
def adventureVer = '4.16.0'
def paperVer = '1.20.6-R0.1-SNAPSHOT'
def commandApiVer = '9.5.0-SNAPSHOT'

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url 'https://repo.opencollab.dev/maven-releases/' }

    // Paper
    maven { url 'https://repo.papermc.io/repository/maven-public/' }

    // Command API Snapshots
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    // McProtocol
    implementation('org.geysermc.mcprotocollib:protocol:' + mcProtocolLibVer)
    implementation('net.kyori:adventure-text-serializer-gson:' + adventureVer)

    // Paper
    compileOnly("io.papermc.paper:paper-api:" + paperVer)

    // Command Api
    implementation("dev.jorel:commandapi-bukkit-shade:" + commandApiVer)
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.version]
}

shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
    relocate("dev.jorel.commandapi", "com.shanebeestudios.mcbots.api.commandapi")
}

build {
    dependsOn(shadowJar)

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from shadowJar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/1-20-6/plugins'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.ShaneBeee'
            artifactId 'StressTestBots'
            version project.version
            artifact shadowJar
            artifact tasks.jar
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/javadoc")
    options {
        links('https://hub.spigotmc.org/javadocs/spigot/')
        links('https://jd.advntr.dev/api/4.14.0/')
        links('https://repo.opencollab.dev/javadoc/maven-snapshots/com/github/steveice10/mcauthlib/1.5-SNAPSHOT/raw')
        links('https://repo.opencollab.dev/javadoc/maven-snapshots/org/geysermc/mcprotocollib/protocol/' + mcProtocolLibVer + '/raw')
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}
