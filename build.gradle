import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.7"
}

group 'com.shanebeestudios'
version '1.0.0'

compileJava   {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url 'https://repo.opencollab.dev/maven-releases/' }

    // Paper
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    // McProtocol
    implementation('com.github.steveice10:mcprotocollib:1.20.4-2-SNAPSHOT')
    implementation('net.kyori:adventure-text-serializer-gson:4.16.0')

    // Paper
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

    // Command Api
    implementation("dev.jorel:commandapi-bukkit-shade:9.3.0")
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.version]
}

shadowJar {
    archiveClassifier = null
    relocate("dev.jorel.commandapi", "com.shanebeestudios.mcbots.api.commandapi")
}

build {
    dependsOn(shadowJar)

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from shadowJar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/1-20-4/plugins'
}
